program->definition-list
definition-list->definition-list definition | definition
definition->variable-definition | function-definition
variable-definition->type-indicator ID SEM | type-indicator ID LS NUM RS SEM
type-indicator->INT | FLOAT | DOUBLE | VOID
function-definition->type-indicator ID LRP parameters RRP compound-stmt
parameters->parameter-list | VOID
parameter-list->parameter-list COMMA parameter | parameter
parameter->type-indicator ID | type-indicator ID LS RS
compound-stmt->LB local-definitions statement-list RB
local-definitions->local-definitions variable-definition | @
statement-list->statement-list statement | @
statement->expression-stmt | compound-stmt | condition-stmt | dowhile-stmt | return-stmt
expression-stmt->expression SEM | SEM
condition-stmt->IF LRP expression RRP statement | IF LRP expression RRP statement ELSE statement
dowhile-stmt->DO statement WHILE LRP expression RRP SEM
return-stmt->RETURN SEM | RETURN expression SEM
expression->variable ASSIGN expression | simple-expression
variable->ID | ID LS expression RS
simple-expression->additive-expression relop additive-expression | additive-expression
relop->LE | LT | GT | GE | EQ | NE
additive-expression->additive-expression addop term | term
addop->PLUS | MINUS
term->term mulop factor | factor
mulop->MUL | DIV | MOD
factor->LRP expression RRP | variable | call | NUM
call->ID LRP arguments RRP
arguments->argument-list | @
argument-list->argument-list COMMA expression | expression